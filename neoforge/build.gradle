plugins {
    id 'com.gradleup.shadow'
    id 'me.shedaniel.unified-publishing'
    id 'org.moddedmc.wiki.toolkit'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    runs {
        dataClient {
            clientData()
            programArgs '--all', '--mod', 'better_mcdonalds_mod'
            programArgs '--output', project(':common').file('src/main/generated').absolutePath
            programArgs '--existing', project(':common').file('src/main/resources').absolutePath
        }

        exportClient {
            client()
            property 'wiki_exporter.enabled', 'true'
        }

        exportServer {
            server()
            programArgs 'nogui'
            property 'wiki_exporter.enabled', 'true'
        }
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }

    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven { url 'https://maven.neoforged.net/releases/' }
    maven { url 'https://maven.sinytra.org' }
}

dependencies {
    neoForge "net.neoforged:neoforge:${project.neoforge_version}"

    modApi "dev.architectury:architectury-neoforge:${project.architectury_api_version}"
    modRuntimeOnly "org.sinytra:wiki-exporter-neoforge:${project.wiki_exporter_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    var replaceProperties = [
            mod_version             : mod_version,
            minecraft_version       : minecraft_version,
            architectury_api_version: architectury_api_version
    ]
    inputs.properties replaceProperties

    filesMatching('META-INF/neoforge.mods.toml') {
        expand replaceProperties
    }
}

shadowJar {
    exclude '**/better_mcdonalds_mod/datagen/**'
    exclude '.cache/**'
    from project(':').file('LICENSE.md')

    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}

unifiedPublishing {
    project {
        displayName = "[NeoForge ${project.minecraft_version}] Better McDonald's Mod ${project.version}"
        version = "${project.version}"
        releaseType = 'release'
        gameVersions = ["${project.minecraft_version}"]
        gameLoaders = ['neoforge']
        changelog = project(':').file('changelog.md').text

        mainPublication tasks.remapJar

        relations {
            depends {
                curseforge = 'architectury-api'
                modrinth = 'architectury-api'
            }
        }

        if (curseforgeApiKey != null) {
            curseforge {
                token = curseforgeApiKey
                id = '546816'
            }
        }

        if (modrinthApiKey != null) {
            modrinth {
                token = modrinthApiKey
                id = 'kdnePgr1'
            }
        }
    }
}

wiki {
    docs {
        'better-mcdonalds-mod' {
            root = project(':').file('docs/better-mcdonalds-mod')
        }
    }
}