plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "me.shedaniel.unified-publishing" version "0.1.12"
}

base {
    archivesName = archives_base_name + "-neoforge"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    runs {
        data {
            data()
            programArgs "--all", "--mod", "better_mcdonalds_mod"
            programArgs "--output", project(":common").file("src/generated").absolutePath
            programArgs "--existing", project(":common").file("src/main/resources").absolutePath
        }
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

repositories {
    maven { url "https://maven.neoforged.net/releases/" }
}

dependencies {
    neoForge "net.neoforged:neoforge:${neoforge_version}"

    modApi "dev.architectury:architectury-neoforge:${architectury_api_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionNeoForge")) { transitive = false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/neoforge.mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "**/better_mcdonalds_mod/datagen/**"
    exclude ".cache/**"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenNeoForge(MavenPublication) {
            artifactId = archives_base_name + "-" + project.name
            from components.java
        }
    }
}

unifiedPublishing {
    project {
        displayName = "[NeoForge ${project.minecraft_version}] Better McDonald's Mod ${project.version}"
        version = "${project.version}"
        releaseType = "release"
        gameVersions = ["${project.minecraft_version}"]
        gameLoaders = ["neoforge"]
        changelog = project(":").file("changelog.md").text

        mainPublication tasks.remapJar

        relations {
            depends {
                curseforge = "architectury-api"
                modrinth = "architectury-api"
            }
        }

        if (curseforgeApiKey != null) {
            curseforge {
                token = curseforgeApiKey
                id = "546816"
            }
        }

        if (modrinthApiKey != null) {
            modrinth {
                token = modrinthApiKey
                id = "kdnePgr1"
            }
        }
    }
}