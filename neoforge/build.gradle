plugins {
    id 'me.shedaniel.unified-publishing'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }

    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven { url 'https://maven.neoforged.net/releases/' }
}

dependencies {
    neoForge "net.neoforged:neoforge:${project.neoforge_version}"

    modApi "dev.architectury:architectury-neoforge:${project.architectury_api_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

jar {
    exclude '.cache/**'
    from project(':').file('LICENSE.md')
}

remapJar {
    input.set tasks.jar.archiveFile
}

unifiedPublishing {
    project {
        displayName = "[NeoForge ${project.minecraft_version}] Better McDonald's Mod ${project.version}"
        version = "${project.version}"
        releaseType = 'release'
        gameVersions = ["${project.minecraft_version}"]
        gameLoaders = ['neoforge']
        changelog = project(':').file('changelog.md').text

        mainPublication tasks.remapJar

        relations {
            depends {
                curseforge = 'architectury-api'
                modrinth = 'architectury-api'
            }
        }

        if (curseforgeApiKey != null) {
            curseforge {
                token = curseforgeApiKey
                id = '546816'
            }
        }

        if (modrinthApiKey != null) {
            modrinth {
                token = modrinthApiKey
                id = 'kdnePgr1'
            }
        }
    }
}